Listing of: osakesaastajat.prg


5 PRINT"{yellow}{clr}":POKE53281,0:POKE53280,15 
6 INPUT"MONTA PELAAJAA";P 
10 DIMA$(12),A(12),B$(P),C(P),D(P,12),X(12) 
20 FORI=1TOP 
30 PRINT"PELAAJAN"I"NIMI":INPUTB$(I) 
40 IF LEN(B$(I))>6THENPRINT"6 KIRJAINTA":GOTO30 
50 C(I)=1200:NEXT 
60 FORI=1TO12 
70 PRINT"YHTION"I"NIMI":INPUTA$(I) 
80 IFLEN(A$(I))>9THEN70 
90 A(I)=100:NEXT 
100 FORT=1TOP 
110 PRINT"{clr}YHTIO        M.  OKRS  MKRS   N:O" 
120 PRINT"{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}" 
121 FORI=1TO12 
130 PRINTA$(I)TAB(15)X(I);A(I)INT(A(I)/6*5)TAB(18)I 
140 NEXT 
150 PRINT"{rvson}TEKEEKO  "B$(T):PRINT"{rvsoff}OSTOJA ?":PRINT"VARAT"C(T) 
160 INPUTI$ 
170 IFI$="K"THEN200 
180 IFI$="E"THEN260 
190 GOTO160 
200 INPUT"N:0";Q:IFQ<1ORQ>12THENGOTO200 
210 INPUT"KPL";W 
220 IFC(T)-W*A(Q)<0THENPRINT"EI VARAA":GOTO200 
230 C(T)=C(T)-W*A(Q) 
240 D(T,Q)=D(T,Q)+W 
250 IFC(T)>0THEN110 
260 PRINT"{clr}PELAAJAN OSAKKEET"T"OSAKKEET" 
265 PRINT"N:0    NIMI     KPL    MKRS":PRINT"{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}{B7}":AX=0 
270 FORI=1TO12:IFD(T,I)>0THENPRINTI;A$(I)TAB(12)D(T,I),INT(A(I)/6*5):AX=1 
280 NEXT 
290 IFAX=0THENPRINT"OSAKKEITA EI OLE":GOSUB1000:GOTO430 
300 PRINT"MYYTKO" 
310 INPUTI$ 
320 IFI$="K"THEN350 
330 IFI$="E"THEN430 
340 GOTO310 
350 INPUT"NRO";Q:IFQ<1ORQ>12THENGOTO350 
360 INPUT"KPL";W 
370 IFW>D(T,Q)THENPRINT"EI OLE":GOTO350 
380 D(T,Q)=D(T,Q)-W 
390 C(T)=C(T)+INT(W*((A(Q)/6)*5)) 
400 PRINT"SAATU"INT(W*(A(Q)/6*5))"MK." 
410 PRINT"PAAOMA"C(T)"MK." 
420 GOSUB1000 
430 NEXT 
440 PRINT"{clr}OSINGOT" 
450 FORT=1TOP 
460 PRINTB$(T)":LLE :" 
470 FORI=1TO12 
475 O=INT(15*RND(TI)+1) 
476 O=O+10 
480 IFD(T,I)>0THENPRINTA$(I)"-"TAB(10)D(T,I)*INT(A(I)/100*O)"MK",A(I)/100*O"%" 
481 IFD(T,I)>0THENC(T)=C(T)+D(T,I)*INT(A(I)/O) 
490 NEXT:NEXT 
500 GOSUB1000 
505 GOSUB700 
510 FORI=1TO12 
520 X(I)=INT(RND(TI)*7)-3 
530 A(I)=A(I)+10*X(I) 
540 IFA(I)>200THENGOSUB580 
550 IFA(I)<10THENGOSUB640 
560 NEXT 
570 GOTO100 
580 PRINT"{clr}OSAKEANTI!" 
590 PRINTA$(I)"KAKSINKERTAISTAA":PRINT"OSAKKEENSA" 
600 FORT=1TOP:D(T,I)=D(T,I)*2:A(I)=100 
620 NEXT 
625 GOSUB1000 
630 RETURN 
640 PRINT"{clr}"A$(I)" TEKI KONKAN." 
650 PRINT"MENETITTE OSAKKEENNE" 
660 FORT=1TOP 
670 D(T,I)=0:A(I)=100 
680 NEXT 
685 GOSUB1000 
690 RETURN 
700 PRINT"{clr}LOPETAMMEKO" 
710 INPUTI$ 
720 IFI$="K"THEN800 
730 IFI$="E"THENRETURN 
740 GOTO710 
800 PRINT"{clr}VAROJEN LASKENTA":FORT=1TO1500:NEXT 
810 FORT=1TOP:PRINTB$(T)"N VARAT" 
820 V(T)=V(T)+C(T) 
830 FORI=1TO12 
840 IFD(T,I)>0THEN860 
850 NEXT:PRINT"PAAOMA"V(T):GOSUB1000:NEXT:GOTO890 
860 PRINTA$(I);":"TAB(9)D(T,I)*A(I);"MK" 
880 V(T)=V(T)+D(T,I)*A(I):GOTO850 
890 PRINT"{clr}LASKENTA SUORITETTU" 
900 FORT=1TOP 
910 PRINT"{down}"B$(T)":N VARAT =",V(T) 
920 NEXT:END 
1000 PRINT"PAINA NAPPIA" 
1010 GETAX$:IFAX$=""THEN1010 
1020 RETURN 
